
AC_REVISION($Revision: 1.11 $)

AC_INIT(lib/dns/name.c)
AC_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST

AC_PROG_CC

AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h)

AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME

AC_CHECK_LIB(pthread, pthread_create,,
             AC_CHECK_LIB(pthread, __pthread_create)
)
AC_CHECK_LIB(xnet, socket,,
	     AC_CHECK_LIB(socket, socket)
	     AC_CHECK_LIB(nsl, inet_ntoa)
)

AC_CHECK_FUNC(inet_ntop, ,
	      [ISC_EXTRA_OBJS="$ISC_EXTRA_OBJS inet_ntop.o"
	       AC_SUBST(ISC_EXTRA_OBJS)
	       AC_DEFINE(NEED_INET_NTOP)]
)
AC_CHECK_FUNC(inet_aton, ,
	      [ISC_EXTRA_OBJS="$ISC_EXTRA_OBJS inet_aton.o"
	       AC_SUBST(ISC_EXTRA_OBJS)
	       AC_DEFINE(NEED_INET_ATON)]
)

AC_PROG_MAKE_SET
AC_PROG_RANLIB
AC_PROG_INSTALL

AC_SUBST(STD_CINCLUDES)
AC_SUBST(STD_CDEFINES)
AC_SUBST(STD_CWARNINGS)
if test "X$GCC" = "Xyes"; then
	STD_CINCLUDES=
	STD_CDEFINES=
	STD_CWARNINGS="-W -Wall -Wmissing-prototypes"
	case "$host" in
		*-sun-solaris*)
			LIBS="$LIBS -lthread"
			;;
	esac
else
	STD_CINCLUDES=
	STD_CDEFINES=
	STD_CWARNINGS=
	case "$host" in
		*-dec-osf*)
			CC="$CC -pthread"
			;;
		*-sun-solaris*)
			CC="$CC -mt"
			;;
	esac
fi

AC_SUBST_FILE(BIND9_MAKE_RULES)
BIND9_MAKE_RULES=make/rules

AC_SUBST_FILE(BIND9_VERSION)
BIND9_VERSION=$srcdir/version

AC_OUTPUT(
	make/rules
	Makefile
	make/Makefile
	lib/Makefile
	lib/isc/Makefile
	lib/isc/include/Makefile
	lib/isc/include/isc/Makefile
	lib/isc/unix/Makefile
	lib/isc/unix/include/Makefile
	lib/isc/unix/include/isc/Makefile
	lib/isc/pthreads/Makefile
	lib/isc/pthreads/include/Makefile
	lib/isc/pthreads/include/isc/Makefile
	lib/dns/Makefile
	lib/dns/include/Makefile
	lib/dns/include/dns/Makefile
	bin/Makefile
	bin/tests/Makefile
	bin/named/Makefile
)
